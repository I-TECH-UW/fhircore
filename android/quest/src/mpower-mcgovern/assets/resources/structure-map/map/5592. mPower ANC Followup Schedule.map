map "http://hl7.org/fhir/StructureMap/5592" = 'mPower ANC Followup Schedule'

uses "http://hl7.org/fhir/StructureDefinition/Parameters" as source
uses "http://hl7.org/fhir/StructureDefinition/CarePlan" as target

group mPowerANCFollowupAndVisit(source src : Parameters, target tgt: CarePlan) {
    src -> evaluate(src, $this.parameter.where(name='subject').resource) as subject,
            evaluate(src, $this.parameter.where(name='definition').resource) as definition,
            evaluate(src, $this.parameter.where(name='period').value) as period
            then ExtractTask(period, subject, definition, tgt) "r_careplan";
}

group ExtractTask(
    source period: Period,
    source subject : Patient,
    source definition: ActivityDefinition,
    target careplan: CarePlan){

    // fill task into careplan contained and add reference in activity.outcome
    subject -> create('Task') as task then {
        subject -> task.id = uuid(),
               task.identifier = create('Identifier') as iden, iden.value = uuid(), iden.use = 'official',
               task.identifier = create('Identifier') as iden, iden.value = 'anc_follow_visit', iden.use = 'secondary',
               task.status = 'requested',
               task.intent = 'plan',
               task.executionPeriod = period,
               task.priority = 'routine',
               task.description = evaluate(definition, $this.title),
               task.for = create('Reference') as ref, ref.reference = evaluate(subject, $this.id.replaceMatches('/_history/.*', '')),
               task.authoredOn = evaluate(subject, now()),
               task.requester = evaluate(subject, $this.generalPractitioner.first()),
               task.owner = evaluate(subject, $this.generalPractitioner.first()) "r_task_data";

        subject where(careplan.id.exists()) -> task.basedOn = create('Reference') as ref, ref.reference = evaluate(careplan, 'CarePlan/' + $this.id.replaceMatches('/_history/.*', '')) "r_task_based_on";
        subject where(careplan.id.exists().not()) -> task.basedOn = reference(careplan) "r_task_based_on";

        subject then ExtractPeriod(subject, task) "r_task_period_extr";

       subject -> task.groupIdentifier = create('Identifier') as groupIdentifier then {
              subject -> groupIdentifier.value = "anc_follow_visit" "rule_group_identifier_value";
              subject -> groupIdentifier.use = "secondary" "rule_group_identifier_use";
       } "rule_group_identifier";

        // anc followup form
        subject -> task.reasonReference = create('Reference') as ref, ref.reference = 'Questionnaire/5591' "r_task_reason_ref";

        // Add the valueReference
        subject -> task.input = create('Task_Input') as input then {
            subject -> input.value = create('Reference') as valueRef, valueRef.reference = 'PlanDefinition/5593' "r_task_input_value_ref";
            subject -> input.type = create('CodeableConcept') as taskCode then extractTaskCode(subject, taskCode) "r_task_input_cod";
        } "r_task_input";

        // create activity.detail of type/kind Task for this Task if not exists
        subject where(careplan.activity.where(detail.kind = 'Task').exists().not())
                -> careplan.activity = create('CarePlan_Activity') as activity then {
                   subject -> activity.detail = create('CarePlan_ActivityDetail') as det then {
                       subject -> det.kind = 'Task' "r_act_det_data";
                   } "r_act_det";
               } "r_cp_acti";

        // add task to careplan only if its valid and not expired
        subject where(task.executionPeriod.start >= today() or task.executionPeriod.end >= today()) then {
                 subject -> evaluate(careplan, activity.where(detail.kind = 'Task')) as activity,
                            activity.outcomeReference = reference(task) "r_cp_task_ref";
                 subject -> careplan.contained = task  "r_add_task";
        } "r_cp_task";
    } "r_task";
}

group extractTaskCode(source subject : Task,target concept: CodeableConcept){
      subject-> concept.coding = c("http://smartregister.org/", "plan_definition") as coding,
           coding.display = 'Plan Definition Reference' "r_cp_cod_disp";
      subject ->  concept.text = 'Plan Definition Reference' "r_cp_cc_txt";
}

group ExtractPeriod(source subject: Patient, target task: Task) {
    subject -> evaluate(task, $this.executionPeriod.start) as start, evaluate(task, $this.executionPeriod.end) as end then {
        subject -> create('Period') as period then {

         subject -> create('date') as startDate,
                    startDate.value = evaluate(start, $this.value.substring(0,10)),
                    create('date') as maxMonth,
                    maxMonth.value = evaluate(end, $this.value.substring(0,10)),
                    evaluate(maxMonth, $this - '1 \'days\''.toQuantity()) as endDate
                    then ExtractPeriodTime(startDate, endDate, period) "r_task_period_extr";

            subject -> task.executionPeriod = period "r_task_per";
        } "rule_period";
    } "r_per";
}

group ExtractPeriodTime(source start: DateType, source end: DateType, target period: Period) {
    start -> period.start = create('dateTime') as dt,
             dt.value = evaluate(start, $this.value.substring(0,10) + 'T00:00:00.00Z') "r_per_start";

    end -> period.end = create('dateTime') as dt,
           dt.value = evaluate(end, $this.value.substring(0,10) + 'T00:00:00.00Z') "r_per_end";
}