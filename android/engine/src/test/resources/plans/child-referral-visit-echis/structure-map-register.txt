map "http://hl7.org/fhir/StructureMap/0a091446-7326-478f-8dce-85e98ce72210" = "eCHIS Child Referral Task"

uses "http://hl7.org/fhir/StructureDefinition/Parameters" as source
uses "http://hl7.org/fhir/StructureDefinition/CarePlan" as target

group eCHISSickChildReferralClosureTask(source src : Parameters, target tgt: CarePlan) {
    src -> evaluate(src, $this.parameter.where(name='subject').resource) as subject,
           evaluate(src, $this.parameter.where(name='definition').resource) as definition,
           evaluate(subject, today()) as dueDate
           then ExtractTasks(dueDate, subject, tgt) "r_careplan";
}

group ExtractTasks(
    source dueDate: DateType,
    source subject : Patient,
    target careplan: CarePlan){

    subject -> dueDate as start,
                evaluate(subject, today() + '1 \'days\''.toQuantity()) as end,
                create('Period') as period,
                careplan.contained = create('Task') as task then {

        subject then ExtractPeriod(start, end, period) "r_task_period_extr";

        subject -> task.id = uuid(),
               task.identifier = create('Identifier') as iden, iden.value = uuid(), iden.use = 'official',
               task.status = 'ready',
               task.intent = 'plan',
               task.executionPeriod = period,
               task.priority = 'routine',
               task.description = 'Child Referral Task',
               task.for = create('Reference') as ref, ref.reference = evaluate(subject, $this.id.replaceMatches('/_history/.*', '')),
               task.basedOn = reference(careplan),
               task.authoredOn = evaluate(subject, now()),
               task.requester = evaluate(subject, $this.generalPractitioner.first()),
               task.focus = create('Reference') as ref, ref.reference = 'PlanDefinition/7d23b73f-dcd4-49d7-ba4c-3f1905b277ee',
               task.owner = evaluate(subject, $this.generalPractitioner.first()) "r_task_data";

        // child referral form
        subject -> task.reasonReference = create('Reference') as ref, ref.reference = 'Questionnaire/eb26579a-2f37-4f75-9783-85987704fa61' "r_task_reason_ref";
    } "r_cp_acti_outcome";
}

group ExtractPeriod(source start: DateType, source end: DateType, target period: Period) {
    start -> period.start = create('dateTime') as dt,
             dt.value = evaluate(start, $this.value.substring(0,10) + 'T00:00:00.00Z') "r_per_start";

    end -> period.end = create('dateTime') as dt,
           dt.value = evaluate(end, $this.value.substring(0,10) + 'T00:00:00.00Z') "r_per_end";
}
