map "http://hl7.org/fhir/StructureMap/627ee8a2-217c-4b53-9047-fb02d2587089" = 'eCHIS Sick Child Treatment Schedule'

uses "http://hl7.org/fhir/StructureDefinition/Parameters" as source
uses "http://hl7.org/fhir/StructureDefinition/careplan" as target

group SickChildTreatment(source src : Parameters, target tgt: careplan) {
  src -> evaluate(src, $this.parameter.where(name='subject').resource) as subject,
              evaluate(src, $this.parameter.where(name='definition').resource) as definition,
              evaluate(subject, today() + '1 \'days\''.toQuantity()) as dueDate
              then ExtractTasks(dueDate, subject, tgt) "r_careplan";
}

group ExtractTasks(
      source dueDate: DateType,
      source subject : Patient,
      target careplan: CarePlan){

    // todo: fix end as its set in json
    subject -> dueDate as start,
                evaluate(subject, today() + '1 \'months\''.toQuantity()) as end,
                create('Period') as period,
                careplan.contained = create('Task') as task then {

        subject then ExtractPeriod(start, end, period) "r_task_period_extr";

        subject -> task.id = uuid(),
               task.identifier = create('Identifier') as iden, iden.value = uuid(), iden.use = 'official',
               task.status = 'ready',
               task.intent = 'plan',
               task.executionPeriod = period,
               task.priority = 'routine',
               task.description = 'Sick Child Assessment',
               task.for = create('Reference') as ref, ref.reference = evaluate(subject, $this.id.replaceMatches('/_history/.*', '')),
               // should refer to plandefinition since referral has no careplan
               // task.basedOn = create('Reference') as ref, ref.reference = evaluate(careplan, $this.instantiatesCanonical.first()),
               // task.basedOn = reference(careplan),
               task.authoredOn = evaluate(subject, now()),
               task.requester = evaluate(subject, $this.generalPractitioner.first()),
               task.focus = create('Reference') as ref, ref.reference = 'PlanDefinition/7d23b73f-dcd4-49d7-ba4c-3f1905b277ee',
               task.owner = evaluate(subject, $this.generalPractitioner.first()) "r_task_data";

        subject where(careplan.id.exists()) -> task.basedOn = create('Reference') as ref,
            ref.reference = evaluate(careplan, $this.id.replaceMatches('/_history/.*', '')) "r_task_based_on";
        subject where(careplan.id.exists().not()) -> task.basedOn = reference(careplan) "r_task_based_on";

        // tb plandef followup form
        subject -> task.reasonReference = create('Reference') as ref, ref.reference = 'Questionnaire/7479d3d2-d5c4-4ae7-b72c-37f2f9cbacc6' "r_task_reason_ref";
    } "r_cp_acti_outcome";
}

group ExtractPeriod(source start: DateType, source end: DateType, target period: Period) {
    start -> period.start = create('dateTime') as dt,
             dt.value = evaluate(start, $this.value.substring(0,10) + 'T00:00:00.00Z') "r_per_start";

    end -> period.end = create('dateTime') as dt,
           dt.value = evaluate(end, $this.value.substring(0,10) + 'T00:00:00.00Z') "r_per_end";
}
