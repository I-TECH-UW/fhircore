map "http://hl7.org/fhir/StructureMap/56813e89-5fe2-45ae-9567-0abbef745c39" = 'Measles Contraindication IG workflow'

uses "http://hl7.org/fhir/StructureDefinition/Parameters" as source
uses "http://hl7.org/fhir/StructureDefinition/CarePlan" as target

group childImmunizationSchedule(source src : Parameters, target careplan: CarePlan) {
    src -> evaluate(src, $this.parameter.where(name='subject').resource) as subject,
        evaluate(src, $this.parameter.where(name='definition').resource) as definition,
        evaluate(src, $this.parameter.where(name='depends-on').resource.entry.where(resource is QuestionnaireResponse).resource) as questionnaireResponse,
        evaluate(src, $this.parameter.where(name='period').value) as period,
        evaluate(src, $this.parameter.where(name='version').value) as version
        then defineMedicationRequestToExtract(period, version, subject, definition, questionnaireResponse, careplan) "r_careplan";
}

group defineMedicationRequestToExtract(
    source period: Period,
    source version: Integer,
    source subject : Patient,
    source definition: ActivityDefinition,
    source questionnaireResponse: QuestionnaireResponse,
    target careplan: CarePlan) {
    questionnaireResponse.item as item where(linkId = 'contraindications' and answer.exists().not()) then {
        subject then extractMedicationRequestOrder(period, version, subject, definition, questionnaireResponse, careplan) "r_medication_request";
    } "questionnaire_response_item";

    questionnaireResponse.item as questionnaireResponseItem  where(linkId = 'contraindications' and answer.exists()) then {
        questionnaireResponseItem where(linkId = 'contraindications' and ((answer.where(value.code = 'DE165')).exists() or (answer.where(value.code = 'DE166')).exists() or (answer.where(value.code = 'DE167')).exists())) then {
            subject then extractMedicationRequestCancelledOrhold(period, version, subject, definition, questionnaireResponse, careplan) "r_medication_request_contraindication";
        } "questionnaire_response_item_contraindication";
    } "questionnaire_response_item_answer_available";
}

group extractMedicationRequestOrder (
    source period: Period,
    source version: Integer,
    source subject : Patient,
    source definition: ActivityDefinition,
    source questionnaireResponse: QuestionnaireResponse,
    target careplan: CarePlan) {

    subject -> create('MedicationRequest') as medicationRequestOrder then {
        subject -> medicationRequestOrder.id =  create('id') as id then {
                subject -> id.value = evaluate(questionnaireResponse, $this.item.where(linkId = 'c1d8fed1-ff36-43b6-8061-22561154b13b').answer.value) "rule_update_medication_request_value";
            } "rule_update_medication_request_value_id";
        subject -> medicationRequestOrder.basedOn = reference(careplan) "rule_update_flag_basedOn";
        subject -> medicationRequestOrder.status = 'active' "r_medication_request_status";
        subject -> medicationRequestOrder.intent = 'order' "r_medication_request_intent";

        subject -> medicationRequestOrder.supportingInformation = create('Reference') as questionnaireReference, questionnaireReference.reference = 'Questionnaire/d4f9bfbe-b684-4505-9c66-719afd5c9fca' "r_task_reason_reference";

        // create activity.detail of type/kind Task for this Task if not exists
        subject where(careplan.activity.where(detail.kind = 'MedicationRequest').exists().not()) -> careplan.activity = create('CarePlan_Activity') as activity then {
            subject -> activity.detail = create('CarePlan_ActivityDetail') as ActivityDefinition then {
                subject -> ActivityDefinition.kind = 'MedicationRequest' "rule_activity_detail_data";
            } "rule_activity_detail";
        } "rule_careplan_activity";

        // add MR to careplan irrespective of its validity
        subject then {
            subject -> evaluate(careplan, activity.where(detail.kind = 'MedicationRequest')) as activity, activity.outcomeReference = reference(medicationRequestOrder) "rule_careplan_task_reference";
            subject -> careplan.contained = medicationRequestOrder "rule_add_task";
        } "rule_careplan_task";
    } "r_medication_request";
}

group extractMedicationRequestCancelledOrhold(
    source period: Period,
    source version: Integer,
    source subject : Patient,
    source definition: ActivityDefinition,
    source questionnaireResponse: QuestionnaireResponse,
    target careplan: CarePlan) {

    //or answer.where(value.code = 'DE166') or answer.where(value.code = 'DE167')
    subject -> create('MedicationRequest') as medicationRequestCancelledOrHold then {
        subject -> medicationRequestCancelledOrHold.id =  create('id') as id then {
            subject -> id.value = evaluate(questionnaireResponse, $this.item.where(linkId = 'c1d8fed1-ff36-43b6-8061-22561154b13b').answer.value) "rule_update_medication_request_value";
        } "rule_update_medication_request_value_id";
        subject -> medicationRequestCancelledOrHold.basedOn = reference(careplan) "rule_update_flag_basedOn";
        subject -> medicationRequestCancelledOrHold.status = 'stopped' "r_medication_request_status";
        subject -> medicationRequestCancelledOrHold.intent = 'order' "r_medication_request_intent";

        // create activity.detail of type/kind Task for this Task if not exists
        subject where(careplan.activity.where(detail.kind = 'MedicationRequest').exists().not()) -> careplan.activity = create('CarePlan_Activity') as activity then {
            subject -> activity.detail = create('CarePlan_ActivityDetail') as ActivityDefinition then {
                subject -> ActivityDefinition.kind = 'MedicationRequest' "rule_activity_detail_data";
            } "rule_activity_detail";
        } "rule_careplan_activity";

        // add MR to careplan irrespective of its validity
        subject then {
            subject -> evaluate(careplan, activity.where(detail.kind = 'MedicationRequest')) as activity, activity.outcomeReference = reference(medicationRequestCancelledOrHold) "rule_careplan_task_reference";
            subject -> careplan.contained = medicationRequestCancelledOrHold "rule_add_task";
        } "rule_careplan_task";

        subject then extractMedicationRequest(period, version, subject, definition, questionnaireResponse, medicationRequestCancelledOrHold, careplan) "r_medication_request_contraindication";
    } "r_medication_request";
}

group extractMedicationRequest(
    source period: Period,
    source version: Integer,
    source subject : Patient,
    source definition: ActivityDefinition,
    source questionnaireResponse: QuestionnaireResponse,
    source medicationRequestCancelledOrHold: MedicationRequest,
    target careplan: CarePlan) {

    subject -> create('MedicationRequest') as medicationRequest then {
        subject -> medicationRequest.id = uuid(),
            medicationRequest.identifier = create('Identifier') as iden, iden.value = uuid(), iden.use = 'official',
            medicationRequest.subject = create('Reference') as patientReference, patientReference.reference = evaluate(subject, $this.id.replaceMatches('/_history/.*', '')),
            medicationRequest.dispenseRequest = create('Dispense_Request') as dispenseRequest, dispenseRequest.validityPeriod = period,
            medicationRequest.basedOn = reference(medicationRequestCancelledOrHold),
            medicationRequest.status = 'active',
            medicationRequest.intent = 'plan',
            medicationRequest.doNotPerform = true,
            medicationRequest.authoredOn = evaluate(subject, now()),
            medicationRequest.recorder = evaluate(subject, $this.generalPractitioner.first()),
            medicationRequest.medication = evaluate(definition, code),
            medicationRequest.medication = evaluate(definition, product) "r_task_data";

        subject then extractPeriod(subject, medicationRequest) "r_task_period_extraction";

        // create activity.detail of type/kind Task for this Task if not exists
        subject where(careplan.activity.where(detail.kind = 'MedicationRequest').exists().not()) -> careplan.activity = create('CarePlan_Activity') as activity then {
            subject -> activity.detail = create('CarePlan_ActivityDetail') as ActivityDefinition then {
                subject -> ActivityDefinition.kind = 'MedicationRequest' "rule_activity_detail_data";
            } "rule_activity_detail";
        } "rule_careplan_activity";

        // add MR to careplan irrespective of its validity
        subject then {
            subject -> evaluate(careplan, activity.where(detail.kind = 'MedicationRequest')) as activity, activity.outcomeReference = reference(medicationRequest) "rule_careplan_task_reference";
            subject -> careplan.contained = medicationRequest "rule_add_task";
        } "rule_careplan_task";
    } "r_task";
}

group extractPeriod(source subject: Patient, target medicationRequest: MedicationRequest) {
    subject -> evaluate(medicationRequest, $this.dispenseRequest) as dispenseRequest, evaluate(dispenseRequest, $this.validityPeriod.start) as start,
        evaluate(dispenseRequest, $this.validityPeriod.end) as end then {
        subject -> create('Period') as period then {
            subject -> period.start = create('dateTime') as startDateTime,
                startDateTime.value = evaluate(start, $this.value.substring(0,10) + 'T00:00:00.00Z') "rule_period_start";

            subject -> period.end = create('dateTime') as endDateTime,
                endDateTime.value = evaluate(end, $this.value.substring(0,10) + 'T00:00:00.00Z') "rule_period_end";

            subject -> medicationRequest.dispenseRequest = create('Dispense_Request') as dispenseRequest, dispenseRequest.validityPeriod = period "rule_execution_period";
        } "rule_period";
    } "rule_task_execution_period";
}