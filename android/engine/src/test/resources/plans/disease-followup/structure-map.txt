map "https://fhir.labs.smartregister.org/fhir/StructureMap/63752b18-9f0e-48a7-9a21-d3714be6309a" = "eCBIS Disease Routine Visit"

uses "http://hl7.org/fhir/StructureDefinition/Parameters" as source
uses "http://hl7.org/fhir/StructureDefinition/CarePlan" as target

group ecbisDiseaseRegistrationAndVisit(source parameters : Parameters, target carePlan: CarePlan) {
    parameters -> evaluate(parameters, $this.parameter.where(name='subject').resource) as subject,
            evaluate(parameters, $this.parameter.where(name='definition').resource) as definition,
            evaluate(parameters, $this.parameter.where(name='depends-on').resource.entry.where(resource is QuestionnaireResponse).resource) as questionnaireResponse,
            evaluate(parameters, $this.parameter.where(name='period').value) as period then extractTask(period, subject, questionnaireResponse, carePlan) "r_careplan";
}

group extractTask(source period: Period, source subject : Patient, source questionnaireResponse: QuestionnaireResponse, target careplan: CarePlan){

    // fill task into careplan contained and add reference in activity.outcome
    subject -> create('Task') as task then {
        subject -> task.id = uuid(),
            task.identifier = create('Identifier') as iden, iden.value = uuid(), iden.use = 'official',
            task.identifier = create('Identifier') as iden, iden.value = 'disease_followup_visit', iden.use = 'secondary',
            task.status = 'requested',
            task.intent = 'plan',
            task.executionPeriod = period,
            task.priority = 'routine',
            task.description = 'Disease visit',
            task.for = create('Reference') as ref, ref.reference = evaluate(subject, $this.id.replaceMatches('/_history/.*', '')),
            task.basedOn = reference(careplan),
            task.authoredOn = evaluate(subject, now()),
            task.requester = evaluate(subject, $this.generalPractitioner.first()),
            task.owner = evaluate(subject, $this.generalPractitioner.first()) "r_task_data";

        subject then extractPeriod(subject, task) "r_task_period_extr";

        // Disease followup form
        subject -> task.reasonReference = create('Reference') as ref, ref.reference = 'Questionnaire/e14b5743-0a06-4ab5-aaee-ac158d4cb64f' "r_task_reason_ref";

        // Add the valueReference
        subject -> task.input = create('Task_Input') as input then {
            subject -> input.value = create('Reference') as valueRef, valueRef.reference = 'PlanDefinition/07200c16-b2d6-4d7d-a53f-128115b0ab2f' "r_task_input_value_ref";
            subject -> input.type = create('CodeableConcept') as taskCode then extractTaskCode(subject, taskCode) "r_task_input_cod";
        } "r_task_input";

        // create activity.detail of type/kind Task for this Task if not exists
        subject where(careplan.activity.where(detail.kind = 'Task').exists().not()) -> careplan.activity = create('CarePlan_Activity') as activity then {
            subject -> activity.detail = create('CarePlan_ActivityDetail') as activityDetail then {
                subject -> activityDetail.kind = 'Task' "r_act_det_data";
            } "r_act_det";
        } "r_cp_acti";

        // add task to careplan only if its valid and not expired
        subject where(task.executionPeriod.start >= today() or task.executionPeriod.end >= today()) then {
            subject -> evaluate(careplan, activity.where(detail.kind = 'Task')) as activity, activity.outcomeReference = reference(task) "r_cp_task_ref";
            subject -> careplan.contained = task  "r_add_task";
        } "r_cp_task";
    } "r_task";
}

group extractTaskCode(source subject : Task, target concept: CodeableConcept){
      subject-> concept.coding = c("http://smartregister.org/", "plan_definition") as coding, coding.display = 'Plan Definition Reference' "r_cp_cod_disp";
      subject ->  concept.text = 'Plan Definition Reference' "r_cp_cc_txt";
}

group extractPeriod(source subject: Patient, target task: Task) {
    subject -> evaluate(task, $this.executionPeriod.start) as start, evaluate(task, $this.executionPeriod.end) as end then {
        subject -> create('Period') as period then {
            subject -> create('date') as startDate, startDate.value = evaluate(start, $this.value.substring(0,7) + '-01'),
                create('date') as maxMonth, maxMonth.value = evaluate(end, $this.value.substring(0,7) + '-01'),
                evaluate(maxMonth, $this - '1 \'days\''.toQuantity()) as endDate then extractPeriodTime(startDate, endDate, period) "r_task_period_extr";

            subject -> task.executionPeriod = period "r_task_per";
        } "rule_period";
    } "r_per";
}

group extractPeriodTime(source start: DateType, source end: DateType, target period: Period) {
    start -> period.start = create('dateTime') as dt, dt.value = evaluate(start, $this.value.substring(0,10) + 'T00:00:00.00Z') "r_per_start";
    end -> period.end = create('dateTime') as dt, dt.value = evaluate(end, $this.value.substring(0,10) + 'T00:00:00.00Z') "r_per_end";
}